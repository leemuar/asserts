#Использовать logos

Перем Лог;
Перем мПараметры;
Перем мПараметрыФлаги;
Перем мПозиционныеПараметры;

Перем мКоманды;

Перем мПозицияВСпискеТокенов;
Перем мПозицияПозиционныхПараметров;
Перем мМассивВходныхПараметров;

Процедура ДобавитьПараметр(Знач ИмяПараметра, Знач Пояснение = "") Экспорт
	
	Лог.Отладка("ДобавитьПараметр: ИмяПараметра <"+ИмяПараметра+">");		
	
	ДобавитьПараметрВТаблицу(мПозиционныеПараметры, ИмяПараметра, Пояснение, Ложь);
	
КонецПроцедуры

Процедура ДобавитьИменованныйПараметр(Знач ИмяПараметра, Знач Пояснение = "") Экспорт
	
	Лог.Отладка("ДобавитьИменованныйПараметр: ИмяПараметра <"+ИмяПараметра+">");		
	
	ДобавитьПараметрВТаблицу(мПараметры, ИмяПараметра, Пояснение, Ложь);
	
КонецПроцедуры

Процедура ДобавитьПараметрФлаг(Знач ИмяПараметра, Знач Пояснение = "") Экспорт
	
	Лог.Отладка("ДобавитьПараметрФлаг: ИмяПараметра <"+ИмяПараметра+">");		
	
	ДобавитьПараметрВТаблицу(мПозиционныеПараметры, ИмяПараметра, Пояснение, Истина);
	
КонецПроцедуры

Функция ОписаниеКоманды(Знач ИмяКоманды) Экспорт
	
	НовоеОписание = Новый Структура;
	НовоеОписание.Вставить("Команда", ИмяКоманды);
	НовоеОписание.Вставить("ПозиционныеПараметры", НоваяТаблицаПараметров());
	НовоеОписание.Вставить("ИменованныеПараметры", НоваяТаблицаПараметров());
	//НовоеОписание.Вставить("ПараметрыФлаги", Новый Соответствие);
	
	Возврат НовоеОписание;
	
КонецФункции

Процедура ДобавитьКоманду(Знач ОписаниеКоманды) Экспорт
	
	мКоманды.Вставить(ОписаниеКоманды.Команда, ОписаниеКоманды);
	
КонецПроцедуры

Процедура ДобавитьПозиционныйПараметрКоманды(Знач ОписаниеКоманды, Знач ИмяПараметра, Знач Пояснение = "") Экспорт
	ДобавитьПараметрВТаблицу(ОписаниеКоманды.ПозиционныеПараметры, ИмяПараметра, Пояснение, Ложь);
КонецПроцедуры

Процедура ДобавитьИменованныйПараметрКоманды(Знач ОписаниеКоманды, Знач ИмяПараметра, Знач Пояснение = "") Экспорт
	ДобавитьПараметрВТаблицу(ОписаниеКоманды.ИменованныеПараметры, ИмяПараметра, Пояснение, Ложь);
КонецПроцедуры

Процедура ДобавитьПараметрФлагКоманды(Знач ОписаниеКоманды, Знач ИмяПараметра, Знач Пояснение = "") Экспорт
	ДобавитьПараметрВТаблицу(ОписаниеКоманды.ПозиционныеПараметры, ИмяПараметра, Пояснение, Истина);
КонецПроцедуры

Функция РазобратьКоманду(Знач МассивПараметров) Экспорт
	
	Если МассивПараметров.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеКоманды = мКоманды[МассивПараметров[0]];
	Если ОписаниеКоманды = Неопределено Тогда
		ВызватьИсключение "Неизвестная команда: " + МассивПараметров[0];
	КонецЕсли;
	
	РезультатКоманды = Новый Структура;
	РезультатКоманды.Вставить("Команда", ОписаниеКоманды.Команда);
	РезультатКоманды.Вставить("ЗначенияПараметров", Новый Соответствие);
	
	мМассивВходныхПараметров = МассивПараметров;
	мПозицияВСпискеТокенов = 1;
	мПозицияПозиционныхПараметров = 0;
	
	РезультатКоманды.ЗначенияПараметров = РазобратьАргументы(ОписаниеКоманды.ИменованныеПараметры, ОписаниеКоманды.ПозиционныеПараметры);
	Возврат РезультатКоманды;
	
КонецФункции

Функция Разобрать(Знач ВходнойМассивПараметров) Экспорт
	
	Если Лог.Уровень() = УровниЛога.Отладка Тогда
		Строка = "";
		Для Каждого Параметр Из ВходнойМассивПараметров Цикл
			Строка = Строка + Параметр + " ";
		КонецЦикла;
		Лог.Отладка("ВходнойМассивПараметров <"+СокрЛП(Строка)+">");
	КонецЕсли;
	
	ОписаниеКоманды = Неопределено;
	Если ВходнойМассивПараметров.Количество() > 0 Тогда 
		ОписаниеКоманды = мКоманды[ВходнойМассивПараметров[0]];
	КонецЕсли;
	Если ОписаниеКоманды <> Неопределено Тогда
		Лог.Отладка("Первый параметр совпадает с именем команды. Переход в режим обработки команд");
		Возврат РазобратьКоманду(ВходнойМассивПараметров);
	Иначе
		мМассивВходныхПараметров = Новый Массив;
		Для Каждого Элемент Из ВходнойМассивПараметров Цикл
			мМассивВходныхПараметров.Добавить(Элемент);
		КонецЦикла;
		
		мПозицияПозиционныхПараметров = 0;
		мПозицияВСпискеТокенов        = 0;
		
		Возврат РазобратьАргументы(мПараметры, мПозиционныеПараметры);
	КонецЕсли;
	
КонецФункции


Функция РазобратьАргументы(Знач ИменованныеПараметры, Знач ПозиционныеПараметры)
	
	РезультатРазбора = Новый Соответствие;

	Если мМассивВходныхПараметров.Количество() = 0 Тогда 
		Лог.Отладка("Параметров не передали.");
		Возврат РезультатРазбора;
	КонецЕсли;
	
	Для Каждого СтрПараметр Из ПозиционныеПараметры.НайтиСтроки(Новый Структура("ЭтоФлаг",Истина)) Цикл
		Лог.Отладка("Сбрасываю параметр-флаг: " + СтрПараметр.Имя);
		РезультатРазбора[СтрПараметр.Имя] = Ложь;
	КонецЦикла;

	Пока Истина Цикл
		
		Токен = СледующийТокен();
		Лог.Отладка("Выбран токен: " + Токен);
		Если Токен = Неопределено Тогда
			Лог.Отладка("Закончились токены");
			Прервать;
		КонецЕсли;
		
		Если ЭтоИменованныйПараметр(Токен, ИменованныеПараметры) Тогда
			Лог.Отладка("Это именованный параметр: " + Токен);
			РезультатРазбора[Токен] = СледующийОбязательныйТокен(Токен);
			Лог.Отладка("Нашли значение именованного параметра: " + РезультатРазбора[Токен]);
		ИначеЕсли ЭтоПараметрФлаг(Токен, ПозиционныеПараметры) Тогда
			Лог.Отладка("Это параметр-флаг: " + Токен);
			РезультатРазбора[Токен] = Истина;
			Лог.Отладка("Нашли параметр-флаг: " + РезультатРазбора[Токен]);
		Иначе
			ИмяПараметра = ВыбратьСледующееИмяПозиционногоПараметра(ПозиционныеПараметры);
			Лог.Отладка("Установлено значение позиционного параметра <" + ИмяПараметра + " = " + Токен + ">");
			РезультатРазбора[ИмяПараметра] = Токен;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатРазбора;
	
КонецФункции

Функция СледующийТокен()
	Если мПозицияВСпискеТокенов = мМассивВходныхПараметров.Количество() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Токен = мМассивВходныхПараметров[мПозицияВСпискеТокенов];
	мПозицияВСпискеТокенов = мПозицияВСпискеТокенов + 1;
	
	Возврат Токен;
КонецФункции

Функция СледующийОбязательныйТокен(Знач ИскомыйПараметр)
	Токен = СледующийТокен();
	Если Токен = Неопределено Тогда
		ВызватьИсключение "Ожидается значение параметра " + ИскомыйПараметр;
	КонецЕсли;
	Возврат Токен;
КонецФункции

Функция ЭтоИменованныйПараметр(Знач Токен, Знач ИменованныеПараметры)
	Возврат ИменованныеПараметры.Найти(Токен, "Имя") <> Неопределено;
КонецФункции

Функция ЭтоПараметрФлаг(Знач Токен, Знач ПозиционныеПараметры)
	СтрПараметр = ПозиционныеПараметры.Найти(Токен, "Имя");
	Возврат СтрПараметр <> Неопределено и СтрПараметр.ЭтоФлаг;
КонецФункции

Функция ВыбратьСледующееИмяПозиционногоПараметра(Знач ПозиционныеПараметры)
	Если мПозицияПозиционныхПараметров = ПозиционныеПараметры.Количество() Тогда
		ВызватьИсключение "Ожидается наличие позиционного параметра";
	КонецЕсли;
	
	СтрПараметр = ПозиционныеПараметры[мПозицияПозиционныхПараметров];
	мПозицияПозиционныхПараметров = мПозицияПозиционныхПараметров + 1;
	Возврат СтрПараметр.Имя;
КонецФункции

Процедура Инит()
	Лог = Логирование.ПолучитьЛог("oscript.lib.cmdline");
	
	мПараметры = НоваяТаблицаПараметров(); // Новый Соответствие;
	//мПараметрыФлаги = Новый Соответствие;
	мПозиционныеПараметры = НоваяТаблицаПараметров(); //Новый Массив;
	мКоманды   = Новый Соответствие;
КонецПроцедуры

Функция НоваяТаблицаПараметров()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("ЭтоФлаг");
	Таблица.Колонки.Добавить("Пояснение");
	
	Возврат Таблица;
	
КонецФункции

Процедура ДобавитьПараметрВТаблицу(Знач Таблица, Знач Имя, Знач Пояснение, Знач Флаг)
	СтрПараметр = Таблица.Добавить();
	СтрПараметр.Имя       = Строка(Имя);
	СтрПараметр.ЭтоФлаг   = Флаг;
	СтрПараметр.Пояснение = Пояснение;
КонецПроцедуры

Инит();
