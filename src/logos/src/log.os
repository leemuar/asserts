//////////////////////////////////////////////////////////////////////////
//
// LOGOS: реализация логирования в стиле log4j для OneScript
//
//////////////////////////////////////////////////////////////////////////

Перем мТекущийУровень;
Перем мСпособыВывода;
Перем мСпособВыводаЗаданВручную;

Перем мИдентификатор;

Функция Уровень() Экспорт
	Возврат мТекущийУровень;
КонецФункции

Процедура УстановитьУровень(Знач Уровень) Экспорт
	Если Уровень < 0 или Уровень > УровниЛога.Отключить Тогда
		ВызватьИсключение "Неверное значение аргумента 'Уровень'";
	КонецЕсли;
	
	мТекущийУровень = Уровень;
	
КонецПроцедуры

Процедура Закрыть() Экспорт
	Для Каждого СпособВывода Из мСпособыВывода Цикл
		СпособВывода.Закрыть();
	КонецЦикла;
	мСпособыВывода.Очистить();
КонецПроцедуры

Процедура ДобавитьСпособВывода(Знач СпособВывода) Экспорт

	Если Не мСпособВыводаЗаданВручную Тогда
		мСпособыВывода.Очистить();
		мСпособВыводаЗаданВручную = Истина;
	КонецЕсли;
	
	мСпособыВывода.Добавить(СпособВывода);

КонецПроцедуры

Функция ПолучитьИдентификатор() Экспорт
	Возврат мИдентификатор;
КонецФункции

Процедура Отладка(Знач Сообщение) Экспорт
	Вывести(Сообщение, УровниЛога.Отладка);
КонецПроцедуры

Процедура Информация(Знач Сообщение) Экспорт
	Вывести(Сообщение, УровниЛога.Информация);
КонецПроцедуры

Процедура Предупреждение(Знач Сообщение) Экспорт
	Вывести(Сообщение, УровниЛога.Предупреждение);
КонецПроцедуры

Процедура Ошибка(Знач Сообщение) Экспорт
	Вывести(Сообщение, УровниЛога.Ошибка);
КонецПроцедуры

Процедура КритичнаяОшибка(Знач Сообщение) Экспорт
	Вывести(Сообщение, УровниЛога.КритичнаяОшибка);
КонецПроцедуры

Процедура Вывести(Знач Сообщение, Знач УровеньСообщения)
	
	Если УровеньСообщения >= Уровень() Тогда
		Для Каждого СпособВывода Из мСпособыВывода Цикл
			СпособВывода.Вывести(УровниЛога.НаименованиеУровня(УровеньСообщения) + " - " + Сообщение);
		КонецЦикла;
	КонецЕсли
	
КонецПроцедуры

Процедура Инициализация()
	
	УстановитьУровень(УровниЛога.Информация);
	ИнициализироватьСпособыВывода();
	мИдентификатор = Новый УникальныйИдентификатор;
	
КонецПроцедуры

Процедура ИнициализироватьСпособыВывода()
	
	Каталог = Новый Файл(ТекущийСценарий().Источник).Путь;
	
	ПодключитьСценарий(Каталог + "file-appender.os", "ВыводЛогаВФайл");
	ПодключитьСценарий(Каталог + "console-appender.os", "ВыводЛогаВКонсоль");
	
	мСпособВыводаЗаданВручную = Ложь;
	мСпособыВывода = Новый Массив;
	
	ВыводПоУмолчанию = Новый ВыводЛогаВКонсоль();
	мСпособыВывода.Добавить(ВыводПоУмолчанию);
	
КонецПроцедуры

Инициализация();