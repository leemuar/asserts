
Процедура ДобавитьОписанияКоманд(Знач Парсер) Экспорт
	ДобавитьКомандуBuild(Парсер);
	ДобавитьКомандуPrepare(Парсер);
	ДобавитьКомандуInstall(Парсер);
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// Описания команд

Процедура ДобавитьКомандуPrepare(Знач Парсер)
	Команда = Парсер.ОписаниеКоманды("prepare");
	Парсер.ДобавитьПозиционныйПараметр(Команда, "ИмяПакета");
	Парсер.ДобавитьИменованныйПараметр(Команда, "dir");
	Парсер.ДобавитьКоманду(Команда);
КонецПроцедуры

Процедура ДобавитьКомандуBuild(Знач Парсер)
	Команда = Парсер.ОписаниеКоманды("build");
	Парсер.ДобавитьПозиционныйПараметр(Команда, "КаталогИсходников");
	Парсер.ДобавитьПозиционныйПараметр(Команда, "ФайлМанифеста");
	Парсер.ДобавитьКоманду(Команда);
КонецПроцедуры

Процедура ДобавитьКомандуInstall(Знач Парсер)
	Команда = Парсер.ОписаниеКоманды("install");
	Парсер.ДобавитьПозиционныйПараметр(Команда, "ИмяПакета");
	Парсер.ДобавитьКоманду(Команда);
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// Реализация команд

Процедура УстановитьПакет(Знач ИмяПакета) Экспорт
	
	Если Прав(НРег(ИмяПакета), 5) = ".ospx" Тогда
		УстановитьПакетИзАрхива(ИмяПакета);
	Иначе
		ВызватьИсключение "Неподдерживаемый тип пакета <" + ИмяПакета + ">";
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПакетИзАрхива(Знач ИмяПакета)
	
	СистемныеБиблиотеки = КаталогСистемныхБиблиотек();
	
	ФайлАрхива = Новый Файл(ИмяПакета);
	ИдентификаторПакета = ФайлАрхива.ИмяБезРасширения;
	
	ПутьУстановки = Новый Файл(ОбъединитьПути(СистемныеБиблиотеки, ИдентификаторПакета));
	Если Не ПутьУстановки.Существует() Тогда
		СоздатьКаталог(ПутьУстановки.ПолноеИмя);
	ИначеЕсли ПутьУстановки.ЭтоФайл() Тогда
		ВызватьИсключение "Не удалось создать каталог " + ПутьУстановки.ПолноеИмя;
	КонецЕсли;
	
	Попытка
		Чтение = Новый ЧтениеZipФайла;
		Чтение.Открыть(ИмяПакета);
		ИмяСкриптаУстановки = "__install__.os";
		ЭлементСкриптаУстановки = Чтение.Элементы.Найти(ИмяСкриптаУстановки);
		Если ЭлементСкриптаУстановки <> Неопределено Тогда
			ВремКаталог = ПолучитьИмяВременногоФайла();
			Чтение.Извлечь(ЭлементСкриптаУстановки, ВремКаталог, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
			ОбъектСкрипта = ЗагрузитьСценарий(ОбъединитьПути(ВремКаталог, ИмяСкриптаУстановки));
			ВызватьСобытиеПриУстановке(ОбъектСкрипта, ПутьУстановки.ПолноеИмя);
			УдалитьФайлы(ВремКаталог);
		КонецЕсли;
		
		Чтение.ИзвлечьВсе(ПутьУстановки.ПолноеИмя);
		
	Исключение
		Чтение.Закрыть();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция КаталогСистемныхБиблиотек()
	СистемныеБиблиотеки = ПолучитьЗначениеСистемнойНастройки("lib.system");
	Если СистемныеБиблиотеки = Неопределено Тогда
		ВызватьИсключение "Не определен каталог системных библиотек";
	КонецЕсли;
	
	Возврат СистемныеБиблиотеки;
	
КонецФункции

Процедура ВызватьСобытиеПриУстановке(Знач ОбъектСкрипта, Знач Каталог)
	Сообщить("Вызываю обработчик ПриУстановке");
	ОбъектСкрипта.ПриУстановке(Каталог);
КонецПроцедуры


Процедура ВыполнитьСборку(Знач КаталогИсходников, Знач ФайлМанифеста) Экспорт
	
	Сборщик = ЗагрузитьСценарий(ОбъединитьПути(КаталогСкрипта(), "builder.os"));
	Сборщик.СобратьПакет(КаталогИсходников, ФайлМанифеста);
	
КонецПроцедуры

Процедура ПодготовитьКаталогПроекта(Знач ИмяПакета, Знач ВыходнойКаталог) Экспорт
	
	Если ВыходнойКаталог = Неопределено Тогда
		ВыходнойКаталог = ТекущийКаталог();
	КонецЕсли;
	
	РабочийКаталог = Новый Файл(ОбъединитьПути(ВыходнойКаталог, ИмяПакета));
	Если Не РабочийКаталог.Существует() Тогда
		СоздатьКаталог(РабочийКаталог.ПолноеИмя);
	Иначе
		Содержимое = НайтиФайлы(РабочийКаталог.ПолноеИмя, ПолучитьМаскуВсеФайлы());
		Если Содержимое.Количество() Тогда
			ВызватьИсключение "Каталог проекта " + РабочийКаталог.ПолноеИмя + " уже существует";
		КонецЕсли;
	КонецЕсли;
	
	ЗаписатьЗаготовкуСкриптаУстановки(РабочийКаталог.ПолноеИмя);
	ЗаписатьЗаготовкуМанифестаБиблиотеки(РабочийКаталог.ПолноеИмя, ИмяПакета);

	Сообщить("Готово");
	
КонецПроцедуры

Процедура ЗаписатьЗаготовкуСкриптаУстановки(Знач Каталог)

	ИмяФайла = ОбъединитьПути(Каталог, "__install__.os");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла);
	ЗаписьТекста.ЗаписатьСтроку("///////////////////////////////////////////////////////////////////");
	ЗаписьТекста.ЗаписатьСтроку("// Сценарий установки и удаления пакета с клиентской машины        ");
	ЗаписьТекста.ЗаписатьСтроку("///////////////////////////////////////////////////////////////////
	|
	|");
	
	ЗаписьТекста.ЗаписатьСтроку("// Вызывается пакетным менеджером при установке пакета на клиентскую машину.");
	ЗаписьТекста.ЗаписатьСтроку("// ");
	ЗаписьТекста.ЗаписатьСтроку("// Параметры:");
	ЗаписьТекста.ЗаписатьСтроку("//   КаталогУстановкиПакета - строка. Путь в который пакетный менеджер устанавливает текущий пакет.");
	ЗаписьТекста.ЗаписатьСтроку("// ");
	ЗаписьТекста.ЗаписатьСтроку("Процедура ПриУстановке(Знач КаталогУстановкиПакета) Экспорт");
	ЗаписьТекста.ЗаписатьСтроку("	// TODO: Реализуйте спец. логику установки, если требуется");
	ЗаписьТекста.ЗаписатьСтроку("КонецПроцедуры");
	ЗаписьТекста.ЗаписатьСтроку(Символы.ПС);
	
	ЗаписьТекста.ЗаписатьСтроку("// Вызывается пакетным менеджером при удалении пакета с клиентской машины.");
	ЗаписьТекста.ЗаписатьСтроку("// ");
	ЗаписьТекста.ЗаписатьСтроку("// Параметры:");
	ЗаписьТекста.ЗаписатьСтроку("//   КаталогУстановкиПакета - строка. Каталог в который установлен пакет.");
	ЗаписьТекста.ЗаписатьСтроку("// ");
	ЗаписьТекста.ЗаписатьСтроку("Процедура ПриУдалении(Знач КаталогУстановкиПакета) Экспорт");
	ЗаписьТекста.ЗаписатьСтроку("	// TODO: Реализуйте спец. логику удаления, если требуется");
	ЗаписьТекста.ЗаписатьСтроку("КонецПроцедуры");
	
	ЗаписьТекста.Закрыть();

КонецПроцедуры

Процедура ЗаписатьЗаготовкуМанифестаБиблиотеки(Знач Каталог, Знач ИмяПакета)
	ИмяФайла = ОбъединитьПути(Каталог, "lib.config");
	
	Запись = Новый ЗаписьXML;
	Запись.ОткрытьФайл(ИмяФайла);
	Запись.ЗаписатьОбъявлениеXML();
	Запись.ЗаписатьНачалоЭлемента("package-def");
	Запись.ЗаписатьСоответствиеПространстваИмен("", "http://oscript.io/schemas/package-def/1.0");
	Запись.ЗаписатьАтрибут("name", ИмяПакета);
	Запись.ЗаписатьАтрибут("version", "0.1");
	
	Запись.ЗаписатьНачалоЭлемента("class");
	Запись.ЗаписатьАтрибут("name", "ИмяКлассаВЯзыке");
	Запись.ЗаписатьАтрибут("file", "ПутьКФайлуРеализации.os");
	Запись.ЗаписатьКонецЭлемента();
	
	Запись.ЗаписатьНачалоЭлемента("module");
	Запись.ЗаписатьАтрибут("name", "ИмяобщегоМодуля");
	Запись.ЗаписатьАтрибут("file", "ПутьКФайлуРеализации.os");
	Запись.ЗаписатьКонецЭлемента();
	
	Запись.ЗаписатьКонецЭлемента();
	Запись.Закрыть();
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// Вспомогательные функции

Функция КаталогСкрипта()
	
	Возврат (Новый Файл(ТекущийСценарий().Источник)).Путь;
	
КонецФункции
