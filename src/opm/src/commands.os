
Процедура ДобавитьОписанияКоманд(Знач Парсер) Экспорт
	ДобавитьКомандуBuild(Парсер);
	ДобавитьКомандуPrepare(Парсер);
	ДобавитьКомандуInstall(Парсер);
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// Описания команд

Процедура ДобавитьКомандуPrepare(Знач Парсер)
	Команда = Парсер.ОписаниеКоманды("prepare");
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "КаталогСборкиПакета");
	Парсер.ДобавитьКоманду(Команда);
КонецПроцедуры

Процедура ДобавитьКомандуBuild(Знач Парсер)
	Команда = Парсер.ОписаниеКоманды("build");
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "КаталогСборкиПакета");
	Парсер.ДобавитьИменованныйПараметрКоманды(Команда, "-mf");
	Парсер.ДобавитьКоманду(Команда);
КонецПроцедуры

Процедура ДобавитьКомандуInstall(Знач Парсер)
	Команда = Парсер.ОписаниеКоманды("install");
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ИмяПакета");
	Парсер.ДобавитьКоманду(Команда);
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// Реализация команд

Процедура УстановитьПакет(Знач ИмяПакета) Экспорт
	
	Если Прав(НРег(ИмяПакета), 5) = ".ospx" Тогда
		УстановитьПакетИзАрхива(ИмяПакета);
	Иначе
		ВызватьИсключение "Неподдерживаемый тип пакета <" + ИмяПакета + ">";
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПакетИзАрхива(Знач ИмяПакета)
	
	СистемныеБиблиотеки = КаталогСистемныхБиблиотек();
	
	ФайлАрхива = Новый Файл(ИмяПакета);
	ИдентификаторПакета = ФайлАрхива.ИмяБезРасширения;
	
	ПутьУстановки = Новый Файл(ОбъединитьПути(СистемныеБиблиотеки, ИдентификаторПакета));
	Если Не ПутьУстановки.Существует() Тогда
		СоздатьКаталог(ПутьУстановки.ПолноеИмя);
	ИначеЕсли ПутьУстановки.ЭтоФайл() Тогда
		ВызватьИсключение "Не удалось создать каталог " + ПутьУстановки.ПолноеИмя;
	КонецЕсли;
	
	Попытка
		Чтение = Новый ЧтениеZipФайла;
		Чтение.Открыть(ИмяПакета);
		ИмяСкриптаУстановки = "__install__.os";
		ЭлементСкриптаУстановки = Чтение.Элементы.Найти(ИмяСкриптаУстановки);
		Если ЭлементСкриптаУстановки <> Неопределено Тогда
			ВремКаталог = ПолучитьИмяВременногоФайла();
			Чтение.Извлечь(ЭлементСкриптаУстановки, ВремКаталог, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
			ОбъектСкрипта = ЗагрузитьСценарий(ОбъединитьПути(ВремКаталог, ИмяСкриптаУстановки));
			ВызватьСобытиеПриУстановке(ОбъектСкрипта, ПутьУстановки.ПолноеИмя);
			УдалитьФайлы(ВремКаталог);
		КонецЕсли;
		
		Чтение.ИзвлечьВсе(ПутьУстановки.ПолноеИмя);
		
	Исключение
		Чтение.Закрыть();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Функция КаталогСистемныхБиблиотек()
	СистемныеБиблиотеки = ПолучитьЗначениеСистемнойНастройки("lib.system");
	Если СистемныеБиблиотеки = Неопределено Тогда
		ВызватьИсключение "Не определен каталог системных библиотек";
	КонецЕсли;
	
	Возврат СистемныеБиблиотеки;
	
КонецФункции

Процедура ВызватьСобытиеПриУстановке(Знач ОбъектСкрипта, Знач Каталог)
	Сообщить("Вызываю обработчик ПриУстановке");
	ОбъектСкрипта.ПриУстановке(Каталог);
КонецПроцедуры

Процедура ВыполнитьСборку(Знач КаталогИсходников, Знач ФайлМанифеста) Экспорт
	
	Сборщик = ЗагрузитьСценарий(ОбъединитьПути(КаталогСкрипта(), "builder.os"));
	Сборщик.СобратьПакет(КаталогИсходников, ФайлМанифеста);
	
КонецПроцедуры

Процедура ПодготовитьКаталогПроекта(Знач ВыходнойКаталог) Экспорт
	
	Если ВыходнойКаталог = Неопределено Тогда
		ВыходнойКаталог = ТекущийКаталог();
	КонецЕсли;

	ВыходнойКаталог = Новый Файл(ВыходнойКаталог);
	
	ИмяПакета = ВыходнойКаталог.Имя;

	Если Не ВыходнойКаталог.Существует() Тогда
		Сообщить("Создаю каталог " + ИмяПакета);
		СоздатьКаталог(ВыходнойКаталог.ПолноеИмя);
	Иначе
		Содержимое = НайтиФайлы(ВыходнойКаталог.ПолноеИмя, ПолучитьМаскуВсеФайлы());
		Если Содержимое.Количество() Тогда
			ВызватьИсключение "Каталог проекта " + ВыходнойКаталог.ПолноеИмя + " уже содержит файлы!";
		КонецЕсли;
	КонецЕсли;
	
	СоздатьПодкаталог(ВыходнойКаталог.ПолноеИмя, "src");
	СоздатьПодкаталог(ВыходнойКаталог.ПолноеИмя, "tests");
	СоздатьПодкаталог(ВыходнойКаталог.ПолноеИмя, "docs");
	
	ЗаписатьЗаготовкуСкриптаУстановки(ВыходнойКаталог.ПолноеИмя);
	ЗаписатьЗаготовкуМанифестаБиблиотеки(ВыходнойКаталог.ПолноеИмя, ИмяПакета);

	Сообщить("Готово");
	
КонецПроцедуры

Процедура СоздатьПодкаталог(Знач База, Знач Имя)
	Сообщить("Создаю каталог " + Имя);
	СоздатьКаталог(ОбъединитьПути(База, Имя));
КонецПроцедуры

Процедура ЗаписатьЗаготовкуСкриптаУстановки(Знач Каталог)

	Сообщить("Создаю заготовку скрипта установки/удаления");

	ИмяФайла = ОбъединитьПути(Каталог, "__install__.os");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла);
	ЗаписьТекста.ЗаписатьСтроку("///////////////////////////////////////////////////////////////////");
	ЗаписьТекста.ЗаписатьСтроку("// Сценарий установки и удаления пакета с клиентской машины        ");
	ЗаписьТекста.ЗаписатьСтроку("///////////////////////////////////////////////////////////////////
	|
	|");
	
	ЗаписьТекста.ЗаписатьСтроку("// Вызывается пакетным менеджером при установке пакета на клиентскую машину.");
	ЗаписьТекста.ЗаписатьСтроку("// ");
	ЗаписьТекста.ЗаписатьСтроку("// Параметры:");
	ЗаписьТекста.ЗаписатьСтроку("//   КаталогУстановкиПакета - строка. Путь в который пакетный менеджер устанавливает текущий пакет.");
	ЗаписьТекста.ЗаписатьСтроку("// ");
	ЗаписьТекста.ЗаписатьСтроку("Процедура ПриУстановке(Знач КаталогУстановкиПакета) Экспорт");
	ЗаписьТекста.ЗаписатьСтроку("	// TODO: Реализуйте спец. логику установки, если требуется");
	ЗаписьТекста.ЗаписатьСтроку("КонецПроцедуры");
	ЗаписьТекста.ЗаписатьСтроку(Символы.ПС);
	
	ЗаписьТекста.ЗаписатьСтроку("// Вызывается пакетным менеджером при удалении пакета с клиентской машины.");
	ЗаписьТекста.ЗаписатьСтроку("// ");
	ЗаписьТекста.ЗаписатьСтроку("// Параметры:");
	ЗаписьТекста.ЗаписатьСтроку("//   КаталогУстановкиПакета - строка. Каталог в который установлен пакет.");
	ЗаписьТекста.ЗаписатьСтроку("// ");
	ЗаписьТекста.ЗаписатьСтроку("Процедура ПриУдалении(Знач КаталогУстановкиПакета) Экспорт");
	ЗаписьТекста.ЗаписатьСтроку("	// TODO: Реализуйте спец. логику удаления, если требуется");
	ЗаписьТекста.ЗаписатьСтроку("КонецПроцедуры");
	
	ЗаписьТекста.Закрыть();

КонецПроцедуры

Процедура ЗаписатьЗаготовкуМанифестаБиблиотеки(Знач Каталог, Знач ИмяПакета)
	
	Сообщить("Создаю заготовку описания пакета");
	
	ЗаписьТекста = Новый ЗаписьТекста(ОбъединитьПути(Каталог, "package-def.os"));
	
	ЗаписьТекста.ЗаписатьСтроку("////////////////////////////////////////////////////////////");
	ЗаписьТекста.ЗаписатьСтроку("// Описание пакета для сборки и установки");
	ЗаписьТекста.ЗаписатьСтроку("// Полную документацию см. на hub.oscript.io/packaging");
	ЗаписьТекста.ЗаписатьСтроку("//");
	
	ЗаписьТекста.ЗаписатьСтроку("
	|Процедура ЗаполнитьОписаниеПакета(Знач Описание) Экспорт
	|	
	|	Описание.Имя("""+ИмяПакета+""")
	|			.Версия(""1.0.1"")
	|			.Автор("""")
	|			.АдресАвтора(""author@somemail.com"")
	|			.Описание(""Это очень хороший и нужный пакет программ"")
	|			.ВерсияСреды(""1.0.11"")
	|			//.ЗависитОт(""package1"", "">=2.0"")
	|			//.ЗависитОт(""package2"", "">=1.1"", ""<2.0"")
	|			//.ОпределяетКласс(""УправлениеВселенной"", ""src/universe-mngr.os"")
	|			//.ОпределяетМодуль(""ПолезныеФункции"", ""src/tools.os"")
	|			;
	|
	|КонецПроцедуры");
	
	ЗаписьТекста.Закрыть();
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
// Вспомогательные функции

Функция КаталогСкрипта()
	
	Возврат (Новый Файл(ТекущийСценарий().Источник)).Путь;
	
КонецФункции
