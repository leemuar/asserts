
/////////////////////////////////////////////////////////////////////////////
// Чтение

Процедура ЗаписатьXML(Знач ЗаписьXML, Знач ОписаниеПакета) Экспорт
	
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("opm-metadata");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("", "http://oscript.io/schemas/opm-metadata/1.0");
	
	ЗаписатьСвойстваПакета(ЗаписьXML, ОписаниеПакета);
	ЗаписатьЗависимостиПакета(ЗаписьXML, ОписаниеПакета);
	ЗаписатьИсполняемыеФайлыПакета(ЗаписьXML, ОписаниеПакета);
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ЗаписатьСвойстваПакета(Знач Запись, Знач Манифест)
	
	СоответствиеИменСвойств = Новый Соответствие;
	СоответствиеИменСвойств.Вставить("Имя"   , "name");
	СоответствиеИменСвойств.Вставить("Версия", "version");
	СоответствиеИменСвойств.Вставить("Автор" , "author");
	СоответствиеИменСвойств.Вставить("Описание"   , "description");
	СоответствиеИменСвойств.Вставить("АдресАвтора", "author-email");
	СоответствиеИменСвойств.Вставить("ВерсияСреды", "engine-version");
	
	СвойстваПакета = Манифест.Свойства();
	
	Для Каждого КлючИЗначение Из СвойстваПакета Цикл
		
		Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		СинонимСвойства = СоответствиеИменСвойств[КлючИЗначение.Ключ];
		Если СинонимСвойства = Неопределено Тогда
			ИмяЭлемента = КлючИЗначение.Ключ;
		Иначе
			ИмяЭлемента = СинонимСвойства;
		КонецЕсли;
		
		Запись.ЗаписатьНачалоЭлемента(ИмяЭлемента);
		Запись.ЗаписатьТекст(XMLСтрока(КлючИЗначение.Значение));
		Запись.ЗаписатьКонецЭлемента();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьЗависимостиПакета(Знач Запись, Знач Манифест)
	
	Зависимости = Манифест.Зависимости();
	Если Зависимости.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Зависимость Из Зависимости Цикл
		Запись.ЗаписатьНачалоЭлемента("depends-on");
		Запись.ЗаписатьАтрибут("name", Зависимость.ИмяПакета);
		Если Не ПустаяСтрока(Зависимость.МинимальнаяВерсия) Тогда
			Запись.ЗаписатьАтрибут("version", Зависимость.МинимальнаяВерсия);
		КонецЕсли;
		Если Не ПустаяСтрока(Зависимость.МаксимальнаяВерсия) Тогда
			Запись.ЗаписатьАтрибут("version-max", Зависимость.МаксимальнаяВерсия);
		КонецЕсли;
		Запись.ЗаписатьКонецЭлемента();
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьИсполняемыеФайлыПакета(Знач Запись, Знач Манифест)
	
	ИсполняемыеФайлы = Манифест.ИсполняемыеФайлы();
	Если ИсполняемыеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Файл Из ИсполняемыеФайлы Цикл
		Запись.ЗаписатьНачалоЭлемента("executable");
		Запись.ЗаписатьТекст(Файл);
		Запись.ЗаписатьКонецЭлемента();
	КонецЦикла
	
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////
// Запись

Функция ПрочитатьXML(Знач ЧтениеXML) Экспорт

КонецФункции
